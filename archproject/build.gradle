/* SonarQube for SonarCloud.io. */
plugins {
    id "org.sonarqube" version "2.2"
}

/* Main plugins. */
apply plugin: 'war'
apply plugin: 'eclipse-wtp'
apply plugin: "jacoco"

sourceCompatibility = 1.8
version = '1.0'
jar {
    manifest {
        attributes 	'Implementation-Title': 'Arch Project', 
        			'Implementation-Version': version
    }
}

repositories {
    mavenCentral()
    
    jcenter()
    maven { url 'http://repository.primefaces.org' }
    maven { url 'http://repository.jboss.org/nexus/content/groups/public' }
}

configurations {
    provided
}

sourceSets {
    main.compileClasspath += configurations.provided
    test.compileClasspath += configurations.provided
    test.runtimeClasspath += configurations.provided
}

eclipse.classpath.plusConfigurations += [configurations.provided]

dependencies {

    /* Cryptography API for BCrypt algorithm. */
    compile 'de.svenkubiak:jBCrypt:0.4.1'
    
    /* Primefaces. */
    compile 'org.primefaces:primefaces:6.1'
    
    /* Primeface's pepper-grinder theme. */
    compile 'org.primefaces.themes:pepper-grinder:1.0.10'
    
    /* JUnit for unit testing. */
    testCompile 'junit:junit:4.12'
    
    /* DbUnit for persitence unit testing. */
    testCompile 'org.dbunit:dbunit:2.5.1'
    testCompile 'org.slf4j:slf4j-simple:1.7.13'
    
    /* Hibernate. */
    testCompile 'org.hibernate:hibernate-core:5.0.10.Final'
    
    /* Hibernate provider for JPA. */
    testCompile 'org.hibernate:hibernate-entitymanager:5.0.10.Final'
    
    /* 
     * Hibernate and c3p0 integration for connection pooling. This dependency 
     * automatically downloads the needed c3p0 standalone jars.
     */
    testCompile 'org.hibernate:hibernate-c3p0:5.0.10.Final'
    
    /* Hibernate's dependency. */
    testCompile 'com.fasterxml:classmate:1.3.1'
    
    /* H2 for in memory persistence tests. */
    testCompile 'com.h2database:h2:1.4.196'
    
    /* Mockito for mocking test dependencies. */
    testCompile 'org.mockito:mockito-all:1.10.19'
    
    /* Arquillian for managing the life cycle of the container. */
    testCompile 'org.jboss.arquillian.junit:arquillian-junit-container:1.1.13.Final'
    
    /* Arquillian and Wildfly integration. */
    testCompile 'org.wildfly.arquillian:wildfly-arquillian-container-managed:2.0.2.Final'
    
    /* Arquillian and Gradle integration. */
    testCompile 'org.jboss.shrinkwrap.resolver:shrinkwrap-resolver-gradle-depchain:2.2.6'
    
    /* Arquillian and Maven integration for resolving dependencies at runtime. */
    testCompile 'org.jboss.shrinkwrap.resolver:shrinkwrap-resolver-api-maven:2.2.6'
    testCompile 'org.jboss.shrinkwrap.resolver:shrinkwrap-resolver-impl-maven:2.2.6'
    
    /* Selenium for front-end testing. */
    testCompile 'org.seleniumhq.selenium:selenium-java:3.4.0'
    
    /* Java EE 7 API. */
    provided 'javax:javaee-api:7.0'
        
    /* SLF4J - Logging API. */
    provided 'org.slf4j:slf4j-api:1.7.13'
        
    /* Security framework provided by Wildfly 10.1.0.Final */
    provided 'org.picketbox:picketbox:4.9.4.Final'
} 